// patrouille/build.gradle

// $ gradle drivers

version = '0.1.0-SNAPSHOT'

ext {
    AUTO_IMPORTED_JAR_PREFIX = 'AUTOIMPORTED_'
    materialstoreVersion = '0.3.3'
    javadiffutilsVersion = '4.9'
    junit4ksVersion = '1.6.1'
}

repositories {
    mavenCentral()
    mavenLocal()
}

configurations {
    myconf
}

dependencies {
    myconf group: 'com.kazurayam', name: 'materialstore',
                    version: "${materialstoreVersion}"
    myconf group: 'io.github.java-diff-utils', name: 'java-diff-utils',
                    version: "${javadiffutilsVersion}"
    myconf group: 'com.kazurayam', name: 'junit4ks', version: "${junit4ksVersion}"
}

task drivers {
    doFirst {
        delete fileTree("Drivers") {
            include("**/" + AUTO_IMPORTED_JAR_PREFIX + "*")
        }
    }
    doLast {
        copy { copySpec ->
            copySpec
                .from(project.getConfigurations().getByName("myconf"))
                .into("Drivers")
                .include(
                    "**/materialstore*.jar",
                    "**/java-diff-utils*.jar",
                    "**/junit4ks*.jar",
                    )
                .rename({ s ->
                    AUTO_IMPORTED_JAR_PREFIX + s
                    })
        }
    }
}
